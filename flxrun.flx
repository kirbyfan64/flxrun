open Filename;

gen realpath: string -> string = 'realpath($1.c_str(), NULL)';

proc process(file: string, data: string) {
    var opts = '';
    if data.startswith '#!' do
        data = '\n' + (get$ split_first (data, '\n')).1;
    done
    data = '#line 2 "' + file.realpath + '"' + data;
    regex := RE2 '^//flxrun:(.*)';
    for line in split (data, '\n') perform
        for g in Match (regex, line) perform opts += g.1;
    dir := join (file.dirname, '.flxrun');
    Directory::mkdirs dir;
    tmp := join (dir, file.basename + '.flx');
    if tmp.load != data call save (tmp, data);
    rcmd := tmp + opts.respectful_split + Cons ('flx', Empty[string]);
    for var i in 2 upto System::argc - 1 perform rcmd = System::argv i + rcmd;
    System::exit$ Shell::system$ rcmd.rev;
}

if System::argc < 2 do
    eprintln 'usage: flxrun <file to execute>';
    System::exit 1;
done
file := System::argv 1;
process (file, file.load);
